#nop 房间信息解析模块;

load-module basic/title;

event.Define {map/ArriveDock}       {无参} {$MODULE} {已经抵达码头};
event.Define {map/ArriveCoachPark}  {无参} {$MODULE} {已经抵达马车行};

event.Define {map/fix/room}         {无参} {$MODULE} {修复房间信息};
event.Define {map/fix/obj}          {无参} {$MODULE} {修复物品信息};

event.HandleOnce {map/init} {pkuxkx/map/room} {map} {pkuxkx.map.Room.init};

#alias {pkuxkx.map.Room.init} {
    set area_detail;
};

#alias {map.Room.Watch} {
    #local dungeon  {?:\[(\S{3,10})副本\]|};
    #local nation   {?:[ ]+\[(大宋|大元|大理|大夏)国\]|};
    #local pkzone   {?:[ ]+(杀戮场)|};
    #local terrain  {?:[ ]+\[(都城|城市|城内|村镇|野外|门派|帮派|阴间|(\S+)势力范围)\]|};
    #local save     {?:[ ]+\[(存盘点)\]|};
    #local store    {?:[ ]+\[(玩家储物柜)\]|};
    #local group    {?:[ ]+\[(\S+)\]|};
    #local mark     {?:[ ]+((?:㊣|★|☆|\s)*)|};
    #local undef    {?:[ ]+\[未定义\]|};

    #class map.Room.Watch open;

    #action {{*UTF8}{?:^}{.{1,9}?\S}{$dungeon} -{$nation}{$pkzone}{$terrain}{$save}{$store}{$group}{$mark}{$undef}{| \[副本\]}$} {
        #local ret {@map.GMCP.Confirm{}};

        #if { @isFalse{$gMapRoom[allDone]} } {
            map.Room.getInfo.done;
        };

        #local keep {};
        #if { &ret[gmcp][] > 0 } {
            #local keep[direction]  {here};
            #local keep[gmcp]       {$ret[gmcp]};
        };
        #elseif { "$ret[cmd]" == "look" } {
            #if { "$ret[exit]" != "" } {
                #local keep {
                    {direction} {$ret[exit]}
                };
            };
            #else {
                #local keep {
                    {area}      {$gMapRoom[area]}
                    {node}      {$gMapRoom[node]}
                    {nodeLinks} {$gMapRoom[nodeLinks]}
                    {coach}     {$gMapRoom[coach]}
                    {coachLinks}{$gMapRoom[coachLinks]}
                    {direction} {here}
                    {gmcp}      {$gMapRoom[gmcp]}
                };
            };
        };

        #local room {
            {name}{%%1}
            {dungeon}{%%2}
            {nation}{%%3}
            {pkzone}{@if{{"%%4" == "杀戮场"};true;false}}
            {terrain}{%%5}
            {save}{@if{{"%%7" == "存盘点"};true;false}}
            {store}{@if{{"%%8" == "玩家储物柜"};true;false}}
            {group}{%%9}
            {mark}{@str.Split{{%%10};{{ +}};{;}}}
            {cmd}{$ret[cmd]}
            {look}{$ret[exit]}
            {gmcp}{$ret[gmcp]}
            $keep
        };

        #local room {$room};

        #local here {0};
        #if { "$room[direction]" == "here" } {
            #local here {@map.Here{}};
        };

        #local header {%%99};

        #if { @option.IsEnable{MapDebug} } {
            #local optional {};
            #if { "$room[mark]" != "" }     {#cat optional { <175>标记<299> $room[mark] }};
            #if { @isTrue{$room[save]} }    {#cat optional { <129><存盘点><299>}};
            #if { @isTrue{$room[store]} }   {#cat optional { <139><储物柜><299>}};
            #if { "$room[group]" != "" }    {#cat optional { <175>帮派驻地<299> $room[group]}};
            #if { $here > 0 }               {#cat optional { <129>定位结果<299> <159>@map.Here{RID}<299>}};

            #echo {<175>房间名称<299> %s <175>国家<299> %s <175>地段<299> %s%s}
                {$room[name]} {$room[nation]} {$room[terrain]} $optional;
        };
        #elseif { $here > 0 } {
            #if { "@map.FollowState{}" == "Normal" } {
                #local here {<159>@map.Here{RID}<299>};
            };
            #else {
                #local here {<259>GPS 无信号<299>};
            };
            #local width {@math.Min{@prompt.ScreenWidth{};90}};
            #local space {@str.Space{@math.Eval{$width - @str.Width{{$header}} - @str.Width{$here}}}};
            #echo {%s %s %s} {$header} {$space} {$here};
        };
        #else {
            #echo {%s} {$header};
        };

        #line gag;

        map.Room.getInfo {$room};
    };

    #class map.Room.Watch close;
};

#alias {map.Room.getInfo} {
    #local args {%1};

    #var gMapRoom               {};
    #var gMapRoom[id]           {};                     #nop 房间ID;
    #var gMapRoom[name]         {$args[name]};          #nop 房间名称;
    #var gMapRoom[colorName]    {};                     #nop 带颜色的房间名称;
    #var gMapRoom[dungeon]      {$args[dungeon]};       #nop 副本名称;
    #var gMapRoom[pkzone]       {$args[pkzone]};        #nop 杀戮场标记;
    #var gMapRoom[nation]       {$args[nation]};        #nop 房间国家，大宋/大理/大元/大夏;
    #var gMapRoom[terrain]      {$args[terrain]};       #nop 房间地段，包含村落和城墙;
    #var gMapRoom[save]         {$args[save]};          #nop 是否为存盘点;
    #var gMapRoom[store]        {$args[store]};         #nop 是否为玩家储物柜;
    #var gMapRoom[group]        {$args[group]};         #nop 帮派名称，非帮派驻地留空;
    #var gMapRoom[village]      {};                     #nop 村落名称（仅村落有效）;
    #var gMapRoom[mark]         {$args[mark]};          #nop 帮派名称，非帮派驻地留空;
    #var gMapRoom[prev]         {$args[prev]};          #nop 上一个房间 ID;
    #var gMapRoom[entry]        {$args[entry]};         #nop 房间入口，是用哪个方向指令进入本房间的;
    #var gMapRoom[cmd]          {$args[cmd]};           #nop 本条房间信息是用何种命令获得;
    #var gMapRoom[look]         {$args[look]};          #nop 如果是 look 命令，那么 look 的是哪个方向;
    #var gMapRoom[gmcp]         {$args[gmcp]};          #nop GMCP.Move 信息;
    #var gMapRoom[area]         {$args[area]};          #nop 房间所在区域;
    #var gMapRoom[map]          {};                     #nop 房间小地图;
    #var gMapRoom[maphere]      {};                     #nop 周围地形图;
    #var gMapRoom[desc]         {};                     #nop 房间文字描述;
    #var gMapRoom[descEnd]      {false};                #nop 房间文字描述已结束（目前以空行标识）;
    #var gMapRoom[view]         {};                     #nop 房间的风景（非文字描述）;
    #var gMapRoom[fog]          {false};                #nop 是否有雾;
    #var gMapRoom[exits]        {};                     #nop 出口列表;
    #var gMapRoom[colorExits]   {};                     #nop 带颜色的出口;
    #var gMapRoom[exitHint]     {};                     #nop 出口所连接的房间;
    #var gMapRoom[existShown]   {false};                #nop 出口信息已出现;
    #var gMapRoom[lookable]     {};                     #nop 你可以看看(look)的东西;
    #var gMapRoom[colorItems]   {};                     #nop 带颜色的房间特殊物品;
    #var gMapRoom[getable]      {};                     #nop 你可以获取(get)的东西;
    #var gMapRoom[objs]         {};                     #nop 房间物品，不包含生物;
    #var gMapRoom[npcs]         {};                     #nop 房间NPC， 不包含非生物和玩家;
    #var gMapRoom[players]      {};                     #nop 房间玩家，不包含非生物和NPC;
    #var gMapRoom[dock]         {};                     #nop 是否为渡口;
    #var gMapRoom[funcs]        {};                     #nop 房间功能，领悟，睡觉，吃喝，买卖，等等;
    #var gMapRoom[node]         {$args[node]};          #nop 节点名称，非节点留空;
    #var gMapRoom[nodeLinks]    {$args[nodeLinks]};     #nop 通过本节点可以抵达的节点列表，非节点留空;
    #var gMapRoom[coach]        {$args[coach]};         #nop 马车行名称，非马车行留空;
    #var gMapRoom[coachLinks]   {$args[coachLinks]};    #nop 通过本车行可以抵达的车行列表，非节点留空;
    #var gMapRoom[direction]    {$args[direction]};     #nop 该房间所在的方位，如果是当前位置则为 here;
    #var gMapRoom[allDone]      {false};                #nop 房间信息已收集完毕;

    #local color {@buffer.RawLine{}};
    #replace color {<{/|}H2>} {};
    #replace color {%S - %*$} {&1};
    #replace color {\e[2;37;0m} {};
    #replace color {%S[%u]}   {&1};
    #var gMapRoom[colorName] {$color};

    #class map.Room.getInfo open;

    event.HandleOnce {GA} {map/room} {map} {map.Room.getInfo.done};

    #action {{*UTF8}{^\s{4}((.*)(\p{Han}+).*\([A-Z][a-z A-Z']*\).*)}{|ID=map/Room/getInfo/objs}$} {
        #local obj {@ParseTitle{%%2}};
        event.Emit {map/fix/obj};

        #if { "$obj[title]" == "船老大" } {
            #if { "$gMapRoom[river]" == "长江" } {
                #var gMapRoom[dock] {GuoJiang};
            };
            #elseif { "$gMapRoom[river]" == "黄河" } {
                #var gMapRoom[dock] {GuoHe};
            };
            #else {
                #var gMapRoom[dock] {YellBoat};
            };
            #if { "$obj[type]" != "NPC" } {
                #local obj[type] {NPC};
                #list gMapRoom[npcs] add {{$obj}};
            };
        };

        #list gMapRoom[objs] add {{$obj}};

        #if { @option.IsEnable{MapDebug} } {
            #local type {$obj[type]};
            #switch {"$type"} {
                #case {"NPC"}   {#local type {<134>房间 NPC}};
                #case {"物品"}  {#local type {<164>房间物品}};
                #case {"玩家"}  {#local type {<174>房间玩家}};
                #default        {#local type {<164>房间物品}};
            };
            #if { @isFalse{$gMapRoom[existShown]} } {
                #var gMapRoom[existShown] {true};
                #echo {%s} {<134>房间出口<299> @slist.JoinAs{{@slist.FromList{$gMapRoom[gmcp][出口信息]}};{<179>%s<299>};{ }}};
            };

            #local name {$obj[name]};
            #if { "$obj[colorName]" != "" } {
                #local name {$obj[colorName]};
            };

            #if { "$obj[emoji]" != "" } {
                #local name {$name<299>{<139>$obj[emoji] <299>}};
            };

            #echo {<164>$type<299> %-18s<299> <169>%-16s<299> %s [%s] %s}
                {$name} {$obj[id]} {{$obj[title]}} {$obj[nick]}
                {@slist.Join{{$obj[status1];$obj[status2]}}};

            #if { "$obj[desc]" != "" } {
                #echo {<164>        <070> %s} {$obj[desc]};
            };

            #line gag;
        };
    } {9.9};

    #action {^%+4s你可以看看(look):%*。$} {
        #local lookable {@str.Split{{%%2};{{，|,}}}};
        #var gMapRoom[lookable] {$lookable};

        #local raw {@buffer.RawLine{}};
        #replace raw {<{\e\[([0-9\e\[m;]+)m([^>]+)\e\[2;37;0m}>} {@map.Room.colorItems{colorItems;{{color}{&2}{name}{&3}}}};

        #local item {};
        #foreach {$lookable} {item} {
            #list gMapRoom[objs] add {{
                {id}    {$item}
                {type}  {lookable}
            }};
        };

        #class map.Room.getInfo.map kill;
        #class map.Room.getInfo.desc kill;

        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<171>特殊物品<299> {$lookable}};
            #line gag;
        };
    };

    #action {^%+4s你可以获取(get):%*。$} {
        #local getable {@str.Split{{%%2};{{，|,}}}};
        #var gMapRoom[getable] {$getable};

        #local item {};
        #foreach {$getable} {item} {
            #list gMapRoom[objs] add {{
                {id}    {$item}
                {type}  {getable}
            }};
        };

        #class map.Room.getInfo.map kill;
        #class map.Room.getInfo.desc kill;

        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<171>动态物品<299> {$getable}};
            #line gag;
        };
    };

    #action {^%+4s这里正是{举世闻名|声威赫赫|名震天下}的%*的产业，你可以进店(shop)逛逛。$} {
        #var gMapRoom[group] {%%3};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>帮派驻地<299> $gMapRoom[group]，有商店};
            #line gag;
        };
    } {3};

    #action {^%+4s这里正是%*的产业$} {
        #var gMapRoom[group] {%%2};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>帮派驻地<299> $gMapRoom[group]，无商店};
            #line gag;
        };
    } {4};

    #action {^%+4s这里建起了一大片宅子，气势恢宏，不知道主人是谁。$} {
        #var gMapRoom[group] {UNKNOWN};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>帮派驻地<299> $gMapRoom[group]};
            #line gag;
        };
    } {4};

    #action {^%+4s这里是%*通往外界的唯一道路。$} {
        #var gMapRoom[terrain] {村落出口};
        #var gMapRoom[village] {%%2};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>村落出口<299> <599><119>%%2<299>};
            #line gag;
        };
    };

    #action {^%+4s这里是%+3..4u的一处$gMapRoom[name]。} {
        #var gMapRoom[terrain] {村落内部};
        #var gMapRoom[village] {%%2};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>村落内部<299> %%2};
            #line gag;
        };
    };

    #action {^%+4s这里是%*的一段城墙。$} {
        #var gMapRoom[terrain] {城墙};
        #var gMapRoom[desc]    {@str.Trim{%%0}};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>城    墙<299> %%2};
            #line gag;
        };
    };

    #action {^%+4s这里是一处%*，人迹罕至，也不知道你怎么会来到这里的。$} {
        #var gMapRoom[terrain] {随机地图};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>随机地图<299>};
            #line gag;
        };
    };

    #action {^%+4s这里显然经常有人经过，地上被踩出了路。$} {
        #var gMapRoom[terrain] {迷宫通道};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>迷宫通道<299> <599><129>发现足迹<299>};
            #line gag;
        };
    };

    #action {^%+4s{长江渡船|黄河渡船|渡船|羊皮筏}停在岸边，随时可以出发。$} {
        map.room.mark-river %%2;
    };

    #action {^%+4s{长江渡船|黄河渡船|渡船|羊皮筏}正在驶回，马上就要靠岸了。$} {
        map.room.mark-river %%2;
    };

    #action {^%+4s{长江渡船|黄河渡船|渡船|羊皮筏}刚刚离开驶向对岸，需要一些时间才能回来。$} {
        map.room.mark-river %%2;
    };

    #alias {map.room.mark-river} {
        #local boat {%%1};
        #switch {"$boat"} {
            #case {"长江渡船"}                  {#var gMapRoom[river] {长江}};
            #case {"{黄河渡船|渡船|羊皮筏}"}    {#var gMapRoom[river] {黄河}};
            #default {errLog 发现不明渡船「$boat」，请联系开发者。};
        };
        #var gMapRoom[dock] {YellBoat};
        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<175>大河渡口<299> 此处有「<130>$boat<299>」可供渡过「<120>$gMapRoom[river]<299>」。};
            #line gag;
        };
    };

    #action {^%+4s{这里(?:明显|所有|唯一)的(?:出口|方向)有|浓雾中你觉得似乎有出口通往} %*$} {
        #local desc {%%2};
        #if { "$desc" == "%*浓雾%*" } {
            #var gMapRoom[fog] {true};
            #var gMapRoom[exitStr] {};
        };
        #else {
            #var gMapRoom[exitStr] {%%3};
        };

        #var gMapRoom[existShown] {true};

        #class map.Room.getInfo.map kill;
        #class map.Room.getInfo.desc kill;

        #if { @option.IsEnable{MapDebug} } {#line gag};

        #local raw {@buffer.RawLine{}};
        #var gMapRoom[colorExitStr] {$raw};

        #if { "$gMapRoom[exitStr]" == "%*。" } {
            map.parseExit;
            #return;
        };

        #class map.Room.getInfo.exit open;
        #local exitCharsets {和|、|[a-z A-Z]};
        #action {^{($exitCharsets)+}$} {
            #local raw {@buffer.RawLine{}};
            #cat gMapRoom[exitStr]      {%%%1};
            #cat gMapRoom[colorExitStr] {$raw};
            #if { @option.IsEnable{MapDebug} } {#line gag};
        };

        #action {^{($exitCharsets)*}。$} {
            #class map.Room.getInfo.exit kill;
            #local raw {@buffer.RawLine{}};
            #cat gMapRoom[exitStr]      {%%%1};
            #cat gMapRoom[colorExitStr] {$raw};
            map.parseExit;
        } {4};
        #class map.Room.getInfo.exit close;
    };

    #action {^%+4s这里没有任何明显的出口。$} {
        #class map.Room.getInfo.map kill;
        #class map.Room.getInfo.desc kill;
        #var gMapRoom[existShown] {true};
        #var {gMapRoom[exits]} {};
        #if { @option.IsEnable{MapDebug} } {
            #line gag;
            #echo {%s} {<134>房间出口<299> %%0};
        };
    };

    #action {^%+4s「%*」: %*{|ID=map/Room/getInfo/weather}$} {
        #var gMapRoom[season] {%%2};
        #var gMapRoom[weather] {%%3};

        #class map.Room.getInfo.map kill;
        #class map.Room.getInfo.desc kill;

        #if { @option.IsEnable{MapDebug} } {
            #echo {%s} {<174>时令季节<299> %%2 <174>天气信息<299> %%3};
            #line gag;
        };
    };

    #action {{*UTF8}{^    (\p{Han}+)等(.*)人\(users\)也在此处。$}} {
        #if { @option.IsEnable{MapDebug} } {
            #echo {<174>房间玩家<299> 仍有%s等 %d 人。} {%%2} {@math.ParseCN{%%3}};
            #line gag;
        };
    };

    #alias {map.parseExit} {
        #local exitList {$gMapRoom[exitStr]};
        #unvar gMapRoom[exitStr];

        #replace exitList {、} {;};
        #replace exitList { 和 } {;};
        #replace exitList {。} {};
        #replace exitList { } {};

        #local exitList {@slist.Sort{$exitList}};
        #var gMapRoom[exits] {$exitList};

        #replace gMapRoom[colorExitStr] {{\e\[([0-9\e\[m;]+)m}{[a-z]{2,9}}{\e\[2;37;0m}} {@map.Room.colorItems{colorExits;{{color}{&2}{name}{&3}}}};
        #unvar gMapRoom[colorExitStr];

        #if { @option.IsEnable{MapDebug} } {
            #local exitStr {$gMapRoom[exits]};
            #if { &gMapRoom[exitHint][] > 0 } {
                #local template {VALUE<299>(<129>\@if{{"\$gMapRoom[exitHint][VALUE]" == ""};{N/A};{\$gMapRoom[exitHint][VALUE]}}<299>)};
                #local exitStr {@fp.Transform{{$exitStr};{$template}}};
            };
            #local exitStr {@slist.JoinAs{{$exitStr};{<179>%s<299>};{ }}};
            #echo {%s} {<134>房间出口<299> $exitStr};
        };
    };

    #alias {map.Room.getInfo.done} {
        #local __unused {%%0};
        #class map.Room.getInfo kill;
        #class map.Room.getInfo.map kill;
        #class map.Room.getInfo.desc kill;
        #unvar gMapRoom[descEnd];
        #unvar gMapRoom[existShown];
        #unvar gMapRoom[drawMap];
        #var gMapRoom[allDone] {true};

        #if { @slist.Contains{{$gMapRoom[lookable]};{<ferry>}} } {
            #var gMapRoom[dock] {Ride};
        };

        #if { @slist.Contains{{$gMapRoom[lookable]};{shalou}} } {
            #var gMapRoom[dock] {Shalou};
        };

        #nop 如果有渡船，则一定可以过河，但如果没有船老大，则只能自己喊。;
        #nop 如果有船老大，那么 gMapRoom[dock] 此时应该已经有值了。;
        #if { "$gMapRoom[river]" == "{长江|黄河}" && "$gMapRoom[dock]" == "" } {
            #var gMapRoom[dock] {YellBoat};
        };

        #nop 同步事件以允许插件修改房间信息。;
        event.Emit {map/fix/room};

        #nop 同步事件以确保连续行走时及时处理回调。;
        event.Emit {map/GotRoomInfo};

        #if { @option.IsEnable{DrawRoomMap} } {
            map.Room.DrawMap {$gMapRoom};
        };
    };

    #func {map.Room.colorItems} {
        #local key  {%%1};
        #local data {%%2};
        #replace data[color] {m\e\[} {;};
        #var gMapRoom[$key][$data[name]] {$data[color]};
    };

    #class map.Room.getInfo close;

    #class map.Room.getInfo.map open;

    #action {^{?:\s{8}}%*{|ID=map/Room/getInfo/minimap}$} {
        #local line {%%1};
        #replace line {%+1..u%+1..s$} {&1};
        #cat gMapRoom[map] {|$line|};
        #if { @option.IsEnable{DrawRoomMap} } {
            #line gag;
        };
        #elseif { @option.IsEnable{MapDebug} } {
            #echo {%s} {<174>地图信息<299> %%1};
            #line gag;
        };
    };

    #class map.Room.getInfo.map close;

    #class map.Room.getInfo.desc open;

    #action {^%*{|ID=map/Room/getInfo/others}$} {
        #class map.Room.getInfo.map kill;

        #if { @isTrue{$gMapRoom[descEnd]} } {
            #if { "%%0" == "%s" && "$gMapRoom[view]" == "" } {
                #return;
            };

            #cat gMapRoom[view] {%%0};
            map.Room.ShowView {%%0};
            #return;
        };

        #if { "%%0" == "%s" } {
            #var gMapRoom[descEnd] {true};
            #return;
        };

        #local obj {@ParseTitle{%%0}};
        #if { &obj[] > 0 } {
            #class map.Room.getInfo.desc kill;
            #var gMapRoom[descEnd] {true};
            #showme @buffer.RawLine{};
            #line gag;
            #return;
        };

        #local text {%%0};
        #replace {text} {{*UTF8}{\p{Han}}} {};

        #local origLen {@str.Len{%%0}};
        #local nonHans {@str.Len{$text}};
        #if { $origLen > 5 && $nonHans * 3 / 2 > $origLen } {
            #var gMapRoom[descEnd] {true};
            #cat gMapRoom[view] {%%0};
            map.Room.ShowView {%%0};
        };
        #else {
            #if { @option.IsEnable{DrawRoomMap} && "$gMapRoom[desc]" == "" } {
                map.Room.DrawMap {@map.Here{ROOM}};
            };
            #cat gMapRoom[desc] {@str.Trim{%%0}};
            #if { @option.IsEnable{MapDebug} } {
                #echo {%s} {<172>房间描述<299> %%0};
                #line gag;
            };
        };
    } {9};

    #class map.Room.getInfo.desc close;
};

///=== {
// ## map.Room.GetObj <物品 ID 及数量，表格>
//    从房间捡起物品，但限制背包中的数量，防止过于贪婪。
// };
#alias {map.Room.GetObj} {
    #local dict {%1};
    #local nop {1};
    #local id {};
    #foreach {*dict[]} {id} {
        #local amount {$dict[$id]};
        #if { "@map.Room.GetObjByID{$id}" != "" && @char.backpack.Amount{Item;$id} < $amount } {
            #local nop {0}; get $id;
        };
    };
    #if { ! $nop } {i2};
};

///=== {
// #@ map.Room.exits
//    获取 gMapRoom 变量中的出口信息。结果承诺将按照字母排序。
//    没有 fullme 的时候，移动时看不到出口，但可以从 GMCP 里获取。
// };
#func {map.Room.exits} {
    #local exits {$gMapRoom[exits]};
    #if { "$exits" != "" } {
        #return {$exits};
    };

    #if { &gMapRoom[gmcp][出口信息][] == 0 } {
        #return {};
    };

    #local exits {$gMapRoom[gmcp][出口信息][%*]};
    #return {@slist.Sort{$exits}};
};
